import org.beryx.jlink.JlinkTask
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'application'
    id "org.moditect.gradleplugin" version "1.0.0-rc3"
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.22.3'
}

setVersion '2.2-beta2'

if (!file("${projectDir}/pdxu.properties").exists()) {
    file("${projectDir}/pdxu.properties").write(file("${projectDir}/pdxu.properties.default").getText())
}

java {
    modularity.inferModulePath = true
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

List<String> runArgs = [
        "--add-exports", "javafx.graphics/com.sun.javafx.scene=com.jfoenix",
        "--add-opens", "com.google.gson/com.google.gson.internal=io.sentry",
        "--add-opens", "java.base/java.lang.reflect=com.jfoenix",
        "-Xmx4g"]

application {
    mainModule = 'com.crschnick.pdx_unlimiter.app'
    mainClass = 'com.crschnick.pdx_unlimiter.app.Main'
    applicationDefaultJvmArgs = runArgs
}

javafx {
    version = '15'
    modules = ['javafx.controls', 'javafx.base']
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        url = 'https://maven.nikr.net/'
    }
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'com.jfoenix:jfoenix:9.0.10'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-javafx', version: "11.3.5"
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-materialdesign-pack', version: "11.3.5"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "2.11.3"
    implementation files("$buildDir/generated-modules/com.realityinteractive.imageio.tga-master-SNAPSHOT.jar")
    implementation files("$buildDir/generated-modules/dds-1.0.0.jar")
    implementation files("$buildDir/generated-modules/commons-lang3-3.11.jar")
    implementation files("$buildDir/generated-modules/commons-io-2.8.0.jar")
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.0-alpha1'
    implementation files("$buildDir/generated-modules/sentry-4.0.0-alpha.2.jar")
    implementation files("$buildDir/generated-modules/jnativehook-2.0.2.jar")
    implementation project(':core')
}

addDependenciesModuleInfo {
    overwriteExistingFiles = true
    jdepsExtraArgs = ['-q']
    outputDirectory = file("$buildDir/generated-modules")
    modules {
        module {
            artifact 'com.1stleg:jnativehook:2.0.2'
            moduleInfoSource = '''
                module org.jnativehook {
                    exports org.jnativehook;
                    exports org.jnativehook.keyboard;
                    requires java.logging;
                    requires java.desktop;
                }
            '''
        }
        module {
            artifact 'io.sentry:sentry:4.0.0-alpha.2'
            moduleInfoSource = '''
                module io.sentry {
                    exports io.sentry;
                    opens io.sentry to com.google.gson;
                    
                    exports io.sentry.protocol;
                    opens io.sentry.protocol to com.google.gson;
                    
                    exports io.sentry.adapters;
                    opens io.sentry.adapters to com.google.gson;
                    
                    exports io.sentry.config;
                    opens io.sentry.config to com.google.gson;
                    
                    exports io.sentry.transport;
                    opens io.sentry.transport to com.google.gson;
                    
                    exports io.sentry.util;
                    opens io.sentry.util to com.google.gson;
                    
                    requires com.google.gson;
                }
            '''
        }
        module {
            artifact 'com.github.tmyroadctfig:com.realityinteractive.imageio.tga:master-SNAPSHOT'
            moduleInfoSource = '''
                module com.realityinteractive.imageio.tga {
                    requires java.se;
                    exports com.realityinteractive.imageio.tga;
                }
            '''
        }
        module {
            artifact 'net.nikr:dds:1.0.0'
            moduleInfoSource = '''
                module net.nikr.dds {
                    requires java.se;
                    exports net.nikr.dds;
                    provides javax.imageio.spi.ImageReaderSpi with net.nikr.dds.DDSImageReaderSpi;
                }
            '''
        }
        module {
            artifact 'org.apache.commons:commons-lang3:3.11'
            moduleInfoSource = '''
                module org.apache.commons.lang3 {
                    requires java.se;
                    exports org.apache.commons.lang3;
                    exports org.apache.commons.lang3.function;
                    exports org.apache.commons.lang3.arch;
                    exports org.apache.commons.lang3.reflect;
                }
            '''
        }
        module {
            artifact 'commons-io:commons-io:2.8.0'
            moduleInfoSource = '''
                module org.apache.commons.io {
                    requires java.se;
                    exports org.apache.commons.io;
                    exports org.apache.commons.io.file;
                    exports org.apache.commons.io.filefilter;
                }
            '''
        }
    }
}

task copyModules(type: Copy) {
    into "${buildDir}/modules"
    from configurations.runtimeClasspath
}

task copyOutput(type: Copy) {
    into "${buildDir}/modules"
    from "${buildDir}/libs"
}

jlink {
    imageDir = file("$buildDir/image")
    options = [
            // '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages']
    launcher {
        name = 'pdxu'
        jvmArgs = runArgs
    }

    customImage {
        appModules = [
                'com.crschnick.pdx_unlimiter.app',
                'org.slf4j',
                'org.slf4j.simple',
                'jdk.crypto.ec',
                'org.kordamp.ikonli.materialdesign'
        ]
    }
}

task createImage(type: JlinkTask, dependsOn: [copyModules, copyOutput]) {
}

task writeVersion(type: DefaultTask) {
    doLast {
        mkdir "${buildDir}/image/"
        file("${buildDir}/image/version").write(String.valueOf(version))
    }
}

createImage.finalizedBy(writeVersion)

OperatingSystem os = DefaultNativePlatform.currentOperatingSystem;
task createDist(type: Zip, dependsOn: createImage) {
    destinationDirectory = buildDir
    archivesBaseName = "pdx_unlimiter"
    archiveAppendix = os.windows ? "windows" : "linux"
    archiveVersion = ""
    from "${buildDir}/image"
}
